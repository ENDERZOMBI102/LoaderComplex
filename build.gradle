plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'java'
	id 'idea'
}

configurations {
    shade
}

allprojects {
	repositories {
		mavenLocal()
		maven { url 'https://libraries.minecraft.net' }
		maven { url 'https://repsy.io/mvn/enderzombi102/mc' }
	}
}

subprojects {
	apply plugin: 'java'

	group = rootProject.group
	version = rootProject.version
	archivesBaseName = rootProject.archives_base_name

	configurations {
		jarz {}
	}

	dependencies {
		implementation "org.jetbrains:annotations:$annotations_version"
		if ( project.name != 'loadercomplex-common' ) {
			implementation( project( ':loadercomplex-common' ) ) {
				transitive = false
			}
			implementation "com.enderzombi102:eventsystem:$eventsystem_version"
			implementation "com.enderzombi102.EnderLib:jvm8:$enderlib_version"
		}
	}
}

def subProjects = new ArrayList<>();

for ( Project proj : rootProject.subprojects ) {
	subProjects.add( proj.getName() )
}

dependencies {
	for ( String proj : subProjects ) {
		shade( project( path: proj, configuration: 'jarz' ) ) {
			transitive = false
		}
	}
	shade "com.mojang:brigadier:$brigadier_version"
	shade "blue.endless:jankson:$jankson_version"
	shade "com.enderzombi102:eventsystem:$eventsystem_version"
	shade "com.enderzombi102.EnderLib:jvm8:$enderlib_version"
}

shadowJar {
    configurations = [ project.configurations.shade ]
    from sourceSets.main.allSource
    classifier ''
}

jar {
	from 'LICENSE'
	//noinspection GroovyAssignabilityCheck
    manifest {
		attributes(
                'Specification-Title'     : 'LoaderComplex',
                'Specification-Vendor'    : 'ENDERZOMBI102',
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : name,
                'Implementation-Version'  : version,
                'Implementation-Vendor'   : 'ENDERZOMBI102',
                'Implementation-Timestamp': new Date().format("dd-MM-yyyy'T'HH:mm:ssZ"),
				'ApiVersion': api_version,
//				'FMLCorePlugin': 'com.enderzombi102.loadercomplex.forge12.LoaderComplexCoremod'
        )
    }
}

artifacts {
    archives shadowJar
}
