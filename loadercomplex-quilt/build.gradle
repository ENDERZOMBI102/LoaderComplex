plugins {
	id 'org.quiltmc.loom' version '0.12.23'
}

group = maven_group
version = rootProject.version
archivesBaseName = archives_base_name

repositories {
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
	maven {
		url 'https://maven.quiltmc.org/repository/snapshot'
		content {
			includeGroup 'org.quiltmc.fabric_api_qsl'
			includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
		}
	}
}

loom.runConfigs.configure {
	client {
		runDir = '../run'
	}
	server {
		runDir = '../run'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings( loom.layered {
		addLayer quiltMappings.mappings( "org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2" )
	})
	modImplementation "org.quiltmc:quilt-loader:$loader_version"

	modImplementation "org.quiltmc:qsl:$qsl_version+$minecraft_version-SNAPSHOT"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:$quilted_fapi_version-$minecraft_version-SNAPSHOT"

	modImplementation("com.terraformersmc:modmenu:$modmenu_version") {
		transitive false
	}

}

processResources {
	inputs.property 'version', rootProject.version
	inputs.property 'description', rootProject.description
	inputs.property 'repo_url', rootProject.repo_url
	inputs.property 'loader_version', loader_version

	filesMatching('quilt.mod.json') {
		expand([
			'version': rootProject.version,
			'description': rootProject.description,
			'loader_version': loader_version,
			'repo_url': rootProject.repo_url
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.sourceCompatibility = 17 // for the IDE support
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

artifacts {
	jarz remapJar
}
