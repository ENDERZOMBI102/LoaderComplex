plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
}

//noinspection GroovyAssignabilityCheck
group = project.maven_group

repositories {
	maven {
		name 'legacy-fabric'
		url 'https://maven.legacyfabric.net'
	}
	maven {
		name 'Jitpack'
		url 'https://jitpack.io'
	}
}

loom {
	setIntermediaryUrl(
		'https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar'
	)
}

loom.runConfigs.configure {
	client {
		runDir = rootProject.file('run')
	}
	server {
		runDir = rootProject.file('run')
	}
}

dependencies {
	implementation "com.google.guava:guava:$guava_version"
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modApi "net.fabricmc:fabric-loader:$loader_version"
	compileOnly "com.mojang:brigadier:$brigadier_version"
	modImplementation include("net.legacyfabric.legacy-fabric-api:legacy-fabric-api:1.2.0+1.12.2") {
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-lifecycle-events-v1" )
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-entity-events-v1" )
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-vanilla-command-api-v1" )
	}
}

processResources {
	inputs.property 'version', project.version
	inputs.property 'description', rootProject.description
	inputs.property 'repo_url', rootProject.repo_url

	filesMatching('fabric.mod.json') {
		expand([
				'version': project.version,
				'description': rootProject.description,
				'repo_url': rootProject.repo_url
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.sourceCompatibility = 8 // for the IDE support
	it.options.encoding = 'UTF-8'
	it.options.release.set(8)

	it.javaCompiler = javaToolchains.compilerFor {
		languageVersion = JavaLanguageVersion.of(16)
	}
}

artifacts {
	jarz remapJar
}
